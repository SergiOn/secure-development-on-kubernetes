import com.github.spotbugs.SpotBugsTask

plugins {
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'java'
    id "com.github.spotbugs" version "2.0.0"
    id 'org.owasp.dependencycheck' version "5.0.0"
    id 'com.palantir.docker' version '0.22.1'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

test {
    useJUnitPlatform()
}

dependencyCheck {
    failBuildOnCVSS = 8
    suppressionFile = file('depcheck-suppressions.xml')
}

spotbugs {
    toolVersion = '3.1.12'
}

tasks.withType(SpotBugsTask) {
    reports.xml.enabled = false
    reports.html.enabled = true
}

docker {
    name 'andifalk/initial-spring-boot-app:latest'
    //name 'eu.gcr.io/pa-afa-kubernetes/hello-spring-kubernetes:latest'
    files bootJar.archiveFile
}

check.dependsOn dependencyCheckAnalyze

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.owasp.encoder:encoder:1.2.2'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.9.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}
